plugins {
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id 'maven-publish'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

group 'ch.bildspur'
version '0.3.0'

def String name = 'bildspur-ui'
sourceCompatibility = 1.8

wrapper {
    gradleVersion = '5.6.4'
    distributionUrl = 'https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

javafx {
    version = "11"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // base
    implementation "com.github.cansik:bildspur-base:0.2.0"

    // javafx
    implementation group: 'org.openjfx', name: 'javafx-base', version: '11.0.2'
    implementation group: 'org.openjfx', name: 'javafx-controls', version: '11.0.2'
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.0.0'

    // are they really needed?
    testCompile 'com.google.code.gson:gson:2.8.6'
    testCompile 'com.github.salomonbrys.kotson:kotson:2.5.0'
}

configurations {
    jar.archiveName = "${name}-${version}.jar"
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task fatJar(type: Jar) {
    archiveName = "${name}-${version}.jar"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cansik/${name}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    afterEvaluate {
        publications {
            gpr(MavenPublication) {
                from(components.java)
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = "bildspur UI"
                    description = "UI library for creative projects."
                    url = "https://github.com/cansik/${name}"
                }
            }
        }
    }
}