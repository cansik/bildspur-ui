plugins {
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id 'maven-publish'
    id 'org.openjfx.javafxplugin' version '0.0.12'
}

group 'ch.bildspur'
version '0.4.5'

def name = 'bildspur-ui'
sourceCompatibility = 1.9

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

javafx {
    version = "11"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.3.72'


    // base
    implementation "com.github.cansik:bildspur-base:0.3.5"

    // javafx
    implementation group: 'org.openjfx', name: 'javafx-base', version: '11.0.2'
    implementation group: 'org.openjfx', name: 'javafx-controls', version: '11.0.2'
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.0.0'

    // json
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'
}

configurations {
    jar.archiveName = "${name}-${version}.jar"
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task fatJar(type: Jar) {
    archiveName = "${name}-${version}.jar"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cansik/${name}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    afterEvaluate {
        publications {
            gpr(MavenPublication) {
                from(components.java)
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = "bildspur UI"
                    description = "UI library for creative projects."
                    url = "https://github.com/cansik/${name}"
                }
            }
        }
    }
}
targetCompatibility = JavaVersion.VERSION_1_9
